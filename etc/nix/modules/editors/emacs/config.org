#+title: Doom Configuration
#+author: Ryan King
#+property: header-args:emacs-lisp :tangle yes :comments link
#+property: header-args:elisp :exports code
#+property: header-args :tangle no :results silent :eval no-export
#+startup: fold
* References
** Posts
- [[https://www.mtsolitary.com/20210309194647-my-org-mode-setup/][Mt. Solitarity org/roam setup]]
- [[https://yiming.dev/blog/2018/03/02/my-org-refile-workflow/][Org Refile Workflow]]
- [[https://coredumped.dev/2021/05/26/taking-org-roam-everywhere-with-logseq/][Org Roam + LogSeq]]
- [[https://rgoswami.me/posts/org-note-workflow/][Orgmode Note Workflow]]
- [[https://config.daviwil.com/workflow][Orgmode Workflow]]
** Example Configurations
- [[https://github.com/tecosaur/emacs-config][Tecnosaur's fancy config]]
- [[https://github.com/daviwil/emacs-from-scratch/blob/master/Emacs.org][Emacs from Scratch Config]]
- [[https://github.com/ztlevi/doom-config][ztlevi's blazing fast doom config]]
- [[https://github.com/shaunsingh/nix-darwin-dotfiles][shaunsingh's literate config]]
- [[https://github.com/clintonboys/emacs.d/blob/master/init.org][Mt. Solidarity Org config]]
* Doom File Setup
Setup the Doom config files.
** ~init.el~
The ~init.el~ file contains the doom module definitions. Modules add in packages and configuration.

#+begin_src emacs-lisp :tangle "init.el" :noweb no-export :comments no
;;; init.el -*- lexical-binding: t; -*-

(doom! :completion
       <<doom-completion>>

       :ui
       <<doom-ui>>

       :editor
       <<doom-editor>>

       :emacs
       <<doom-emacs>>

       :term
       <<doom-term>>

       :checkers
       <<doom-checkers>>

       :tools
       <<doom-tools>>

       :os
       <<doom-os>>

       :lang
       <<doom-lang>>

       :config
       <<doom-config>>)
#+end_src
** ~packages.el~
This file configures packages such as adding additional ones or changing versions.

#+begin_src emacs-lisp :tangle "packages.el" :comments no
;;; -*- no-byte-compile: t; -*-
#+end_src

** ~config.el~
This file contains all user configuration.

#+begin_src emacs-lisp :comments no
;;; config.el -*- lexical-binding: t; -*-
#+end_src

** ~cli.el~
The ~doom~ CLI loads this file every time that it runs. Don't require confirmation for org tangle.

#+begin_src emacs-lisp :tangle "cli.el" :comments no
;;; cli.el -*- lexical-binding: t; -*-
(setq org-confirm-babel-evaluate nil)

(defun doom-shut-up-a (orig-fn &rest args)
  (quiet! (apply orig-fn args)))

(advice-add 'org-babel-execute-src-block :around #'doom-shut-up-a)
#+end_src
* Core Configuration
** Personal Information
Set the user information. Nix will template in the git author info at rebuild time.

#+begin_src emacs-lisp
(setq user-full-name "@gitName@"
      user-mail-address "@gitEmail@")
#+end_src

Move cached auth information out of ~$EMACSDIR~.

#+begin_src emacs-lisp
(setq auth-sources '("~/.local/share/doom/authinfo.gpg")
      auth-source-cache-expiry nil)
#+end_src
** Better Defaults
Some simple settings to make things a bit better.

#+begin_src emacs-lisp
(setq-default
 delete-by-moving-to-trash t                      ; Delete files to trash
 window-combination-resize t                      ; take new window space from all other windows (not just current)
 x-stretch-cursor t)                              ; Stretch cursor to the glyph width

(setq undo-limit 80000000                         ; Raise undo-limit to 80Mb
      evil-want-fine-undo t                       ; By default while in insert all changes are one big blob. Be more granular
      auto-save-default t                         ; Nobody likes to loose work, I certainly don't
      truncate-string-ellipsis "…"                ; Unicode ellispis are nicer than "...", and also save /precious/ space
      password-cache-expiry nil                   ; I can trust my computers ... can't I?
      scroll-margin 2                             ; It's nice to maintain a little margin
      evil-vsplit-window-right t                  ; Vertical split to the right
      evil-split-window-below t)                  ; Split below

(global-subword-mode 1)                           ; Iterate through CamelCase words
#+end_src

Use small frame for new Emacs instances.

#+begin_src emacs-lisp
(add-to-list 'default-frame-alist '(height . 24))
(add-to-list 'default-frame-alist '(width . 80))
#+end_src

Put all customization in a separate file.

#+begin_src emacs-lisp
(setq-default custom-file (expand-file-name ".custom.el" doom-private-dir))
(when (file-exists-p custom-file)
  (load custom-file))
#+end_src

** UI Settings
Keep the UI simple and minimalist.

#+begin_src emacs-lisp
(setq fringe-mode 0
      tool-bar-mode 0)
#+end_src
*** Fonts
Set the fonts to the values configured in Nix.

#+begin_src emacs-lisp
(setq doom-font (font-spec :family "@monoFamily@" :weight '@monoWeight@ :size @monoSize@)
      doom-big-font (font-spec :family "@monoFamily@" :weight '@monoWeight@ :size @monoBigSize@)
      doom-unicode-font (font-spec :family "@monoUnicodeFamily@" :weight '@monoWeight@ :size @monoSize@)
      doom-variable-pitch-font (font-spec :family "@sansFamily@" :weight '@sansWeight@ :size @sansSize@)
      doom-serif-font (font-spec :family "@serifFamily@" :weight '@serifWeight@ :size @serifSize@))
#+end_src

Setup mixed pitch for certain modes. Use a hook so that it runs after UI initialization.

#+begin_src emacs-lisp
(defvar mixed-pitch-modes '(org-mode LaTeX-mode markdown-mode gfm-mode Info-mode)
  "Modes to enable `mixed-pitch-mode' in, but only after UI initialisation.")

(defun init-mixed-pitch-h ()
  "Hook `mixed-pitch-mode' into each mode in `mixed-pitch-modes'.
Also immediately enables `mixed-pitch-modes' if currently in one of the modes."
  (when (memq major-mode mixed-pitch-modes)
    (mixed-pitch-mode 1))
  (dolist (hook mixed-pitch-modes)
    (add-hook (intern (concat (symbol-name hook) "-hook")) #'mixed-pitch-mode)))

(add-hook 'doom-init-ui-hook #'init-mixed-pitch-h)
#+end_src

#+begin_src emacs-lisp
(after! mixed-pitch
  (defface variable-pitch-serif
    '((t (:family "serif")))
    "A variable-pitch face with serifs."
    :group 'basic-faces)
  (setq mixed-pitch-set-height t)
  (setq variable-pitch-serif-font (font-spec :family "Alegreya" :size 27))
  (set-face-attribute 'variable-pitch-serif nil :font variable-pitch-serif-font)
  (defun mixed-pitch-serif-mode (&optional arg)
    "Change the default face of the current buffer to a serifed variable pitch, while keeping some faces fixed pitch."
    (interactive)
    (let ((mixed-pitch-face 'variable-pitch-serif))
      (mixed-pitch-mode (or arg 'toggle)))))
#+end_src
*** Themes
The Nix configuration also sets the theme.

#+begin_src emacs-lisp
(setq doom-theme '@theme@)
(setq doom-nord-padded-modeline t)
#+end_src
*** Dashboard
Customize the dashboard key bindings.

#+begin_src emacs-lisp
(map! :map +doom-dashboard-mode-map
      :ne "f" #'find-file
      :ne "r" #'consult-recent-file
      :ne "p" #'doom/open-private-config
      :ne "c" (cmd! (find-file (expand-file-name "config.org" doom-private-dir)))
      :ne "." (cmd! (doom-project-find-file "~/Workshop/"))
      :ne "b" #'+vertico/switch-workspace-buffer
      :ne "B" #'consult-buffer
      :ne "q" #'save-buffers-kill-terminal)
#+end_src
*** Splash Screen
Use a simple splash screen.

#+begin_src emacs-lisp
(defvar fancy-splash-image-template
  (expand-file-name "emacs-e.svg" doom-private-dir)
  "Default template svg used for the splash image, with substitutions from ")

(defvar fancy-splash-sizes
  `((:height 300 :min-height 50 :padding (0 . 2))
    (:height 250 :min-height 42 :padding (2 . 4))
    (:height 200 :min-height 35 :padding (3 . 3))
    (:height 150 :min-height 28 :padding (3 . 3))
    (:height 100 :min-height 20 :padding (2 . 2))
    (:height 75  :min-height 15 :padding (2 . 1))
    (:height 50  :min-height 10 :padding (1 . 0))
    (:height 1   :min-height 0  :padding (0 . 0)))
  "list of plists with the following properties
  :height the height of the image
  :min-height minimum `frame-height' for image
  :padding `+doom-dashboard-banner-padding' (top . bottom) to apply
  :template non-default template file
  :file file to use instead of template")

(defvar fancy-splash-template-colours
  '(("$colour1" . keywords) ("$colour2" . type) ("$colour3" . base5) ("$colour4" . base8))
  "list of colour-replacement alists of the form (\"$placeholder\" . 'theme-colour) which applied the template")

(unless (file-exists-p (expand-file-name "theme-splashes" doom-cache-dir))
  (make-directory (expand-file-name "theme-splashes" doom-cache-dir) t))

(defun fancy-splash-filename (theme-name height)
  (expand-file-name (concat (file-name-as-directory "theme-splashes")
                            theme-name
                            "-" (number-to-string height) ".svg")
                    doom-cache-dir))

(defun fancy-splash-clear-cache ()
  "Delete all cached fancy splash images"
  (interactive)
  (delete-directory (expand-file-name "theme-splashes" doom-cache-dir) t)
  (message "Cache cleared!"))

(defun fancy-splash-generate-image (template height)
  "Read TEMPLATE and create an image if HEIGHT with colour substitutions as
   described by `fancy-splash-template-colours' for the current theme"
  (with-temp-buffer
    (insert-file-contents template)
    (re-search-forward "$height" nil t)
    (replace-match (number-to-string height) nil nil)
    (dolist (substitution fancy-splash-template-colours)
      (goto-char (point-min))
      (while (re-search-forward (car substitution) nil t)
        (replace-match (doom-color (cdr substitution)) nil nil)))
    (write-region nil nil
                  (fancy-splash-filename (symbol-name doom-theme) height) nil nil)))

(defun fancy-splash-generate-images ()
  "Perform `fancy-splash-generate-image' in bulk"
  (dolist (size fancy-splash-sizes)
    (unless (plist-get size :file)
      (fancy-splash-generate-image (or (plist-get size :template)
                                       fancy-splash-image-template)
                                   (plist-get size :height)))))

(defun ensure-theme-splash-images-exist (&optional height)
  (unless (file-exists-p (fancy-splash-filename
                          (symbol-name doom-theme)
                          (or height
                              (plist-get (car fancy-splash-sizes) :height))))
    (fancy-splash-generate-images)))

(defun get-appropriate-splash ()
  (let ((height (frame-height)))
    (cl-some (lambda (size) (when (>= height (plist-get size :min-height)) size))
             fancy-splash-sizes)))

(setq fancy-splash-last-size nil)
(setq fancy-splash-last-theme nil)
(defun set-appropriate-splash (&rest _)
  (let ((appropriate-image (get-appropriate-splash)))
    (unless (and (equal appropriate-image fancy-splash-last-size)
                 (equal doom-theme fancy-splash-last-theme)))
    (unless (plist-get appropriate-image :file)
      (ensure-theme-splash-images-exist (plist-get appropriate-image :height)))
    (setq fancy-splash-image
          (or (plist-get appropriate-image :file)
              (fancy-splash-filename (symbol-name doom-theme) (plist-get appropriate-image :height))))
    (setq +doom-dashboard-banner-padding (plist-get appropriate-image :padding))
    (setq fancy-splash-last-size appropriate-image)
    (setq fancy-splash-last-theme doom-theme)
    (+doom-dashboard-reload)))

(add-hook 'window-size-change-functions #'set-appropriate-splash)
(add-hook 'doom-load-theme-hook #'set-appropriate-splash)
#+end_src

Add an interesting phrase to the splash screen.

#+begin_src emacs-lisp
(defvar splash-phrase-source-folder
  (expand-file-name "misc/splash-phrases" doom-private-dir)
  "A folder of text files with a fun phrase on each line.")

(defvar splash-phrase-sources
  (let* ((files (directory-files splash-phrase-source-folder nil "\\.txt\\'"))
         (sets (delete-dups (mapcar
                             (lambda (file)
                               (replace-regexp-in-string "\\(?:-[0-9]+-\\w+\\)?\\.txt" "" file))
                             files))))
    (mapcar (lambda (sset)
              (cons sset
                    (delq nil (mapcar
                               (lambda (file)
                                 (when (string-match-p (regexp-quote sset) file)
                                   file))
                               files))))
            sets))
  "A list of cons giving the phrase set name, and a list of files which contain phrase components.")

(defvar splash-phrase-set
  (nth (random (length splash-phrase-sources)) (mapcar #'car splash-phrase-sources))
  "The default phrase set. See `splash-phrase-sources'.")

(defun splase-phrase-set-random-set ()
  "Set a new random splash phrase set."
  (interactive)
  (setq splash-phrase-set
        (nth (random (1- (length splash-phrase-sources)))
             (cl-set-difference (mapcar #'car splash-phrase-sources) (list splash-phrase-set))))
  (+doom-dashboard-reload t))

(defvar splase-phrase--cache nil)

(defun splash-phrase-get-from-file (file)
  "Fetch a random line from FILE."
  (let ((lines (or (cdr (assoc file splase-phrase--cache))
                   (cdar (push (cons file
                                     (with-temp-buffer
                                       (insert-file-contents (expand-file-name file splash-phrase-source-folder))
                                       (split-string (string-trim (buffer-string)) "\n")))
                               splase-phrase--cache)))))
    (nth (random (length lines)) lines)))

(defun splash-phrase (&optional set)
  "Construct a splash phrase from SET. See `splash-phrase-sources'."
  (mapconcat
   #'splash-phrase-get-from-file
   (cdr (assoc (or set splash-phrase-set) splash-phrase-sources))
   " "))

(defun doom-dashboard-phrase ()
  "Get a splash phrase, flow it over multiple lines as needed, and make fontify it."
  (mapconcat
   (lambda (line)
     (+doom-dashboard--center
      +doom-dashboard--width
      (with-temp-buffer
        (insert-text-button
         line
         'action
         (lambda (_) (+doom-dashboard-reload t))
         'face 'doom-dashboard-menu-title
         'mouse-face 'doom-dashboard-menu-title
         'help-echo "Random phrase"
         'follow-link t)
        (buffer-string))))
   (split-string
    (with-temp-buffer
      (insert (splash-phrase))
      (setq fill-column (min 70 (/ (* 2 (window-width)) 3)))
      (fill-region (point-min) (point-max))
      (buffer-string))
    "\n")
   "\n"))

(defadvice! doom-dashboard-widget-loaded-with-phrase ()
  :override #'doom-dashboard-widget-loaded
  (setq line-spacing 0.2)
  (insert
   "\n\n"
   (propertize
    (+doom-dashboard--center
     +doom-dashboard--width
     (doom-display-benchmark-h 'return))
    'face 'doom-dashboard-loaded)
   "\n"
   (doom-dashboard-phrase)
   "\n"))
#+end_src

Remove dashboard hooks.

#+begin_src emacs-lisp
(remove-hook '+doom-dashboard-functions #'doom-dashboard-widget-shortmenu)
(add-hook! '+doom-dashboard-mode-hook (hide-mode-line-mode 1) (hl-line-mode -1))
(setq-hook! '+doom-dashboard-mode-hook evil-normal-state-cursor (list nil))
#+end_src

Add ASCII banner for console Emacs.

#+begin_src emacs-lisp
(defun doom-dashboard-draw-ascii-emacs-banner-fn ()
  (let* ((banner
          '(",---.,-.-.,---.,---.,---."
            "|---'| | |,---||    `---."
            "`---'` ' '`---^`---'`---'"))
         (longest-line (apply #'max (mapcar #'length banner))))
    (put-text-property
     (point)
     (dolist (line banner (point))
       (insert (+doom-dashboard--center
                +doom-dashboard--width
                (concat
                 line (make-string (max 0 (- longest-line (length line)))
                                   32)))
               "\n"))
     'face 'doom-dashboard-banner)))

(unless (display-graphic-p) ; for some reason this messes up the graphical splash screen atm
  (setq +doom-dashboard-ascii-banner-fn #'doom-dashboard-draw-ascii-emacs-banner-fn))
#+end_src
*** Misc
Relative line numbers are essential.

#+begin_src emacs-lisp
(setq display-line-numbers-type 'relative)
#+end_src

Better buffer names.

#+begin_src emacs-lisp
(setq doom-fallback-buffer-name "► Doom"
      +doom-dashboard-name "► Doom")
#+end_src

** Keybindings
Use arrow keys instead of ~hjlk~ to support layered Colemak.

#+begin_src emacs-lisp
(map!
 "C-<left>" #'evil-window-left
 "C-<right>" #'evil-window-right
 "C-<up>" #'evil-window-up
 "C-<down>" #'evil-window-down

 "C-S-<left>" #'+evil/window-move-left
 "C-S-<right>" #'+evil/window-move-right
 "C-S-<up>" #'+evil/window-move-up
 "C-S-<down>" #'+evil/window-move-down)
#+end_src
** Asynchronous Configuration Tangling
Use Tecosaur's async config tangling setup.

#+begin_src emacs-lisp
(defvar +literate-tangle--proc nil)
(defvar +literate-tangle--proc-start-time nil)

(defadvice! +literate-tangle-async-h ()
  "A very simplified version of `+literate-tangle-h', but async."
  :override #'+literate-tangle-h
  (unless (getenv "__NOTANGLE")
    (let ((default-directory doom-private-dir))
      (when +literate-tangle--proc
        (message "Killing outdated tangle process...")
        (set-process-sentinel +literate-tangle--proc #'ignore)
        (kill-process +literate-tangle--proc)
        (sit-for 0.3)) ; ensure the message is seen for a bit
      (setq +literate-tangle--proc-start-time (float-time)
            +literate-tangle--proc
            (start-process "tangle-config"
                           (get-buffer-create " *tangle config*")
                           "emacs" "--batch" "--eval"
                           (format "(progn \
(require 'ox) \
(require 'ob-tangle) \
(setq org-confirm-babel-evaluate nil \
      org-inhibit-startup t \
      org-mode-hook nil \
      write-file-functions nil \
      before-save-hook nil \
      after-save-hook nil \
      vc-handled-backends nil \
      org-startup-folded nil \
      org-startup-indented nil) \
(org-babel-tangle-file \"%s\" \"%s\"))"
                                   +literate-config-file
                                   (expand-file-name (concat doom-module-config-file ".el")))))
      (set-process-sentinel +literate-tangle--proc #'+literate-tangle--sentinel)
      (run-at-time nil nil (lambda () (message "Tangling config.org"))) ; ensure shown after a save message
      "Tangling config.org...")))

(defun +literate-tangle--sentinel (process signal)
  (cond
   ((and (eq 'exit (process-status process))
         (= 0 (process-exit-status process)))
    (message "Tangled config.org sucessfully (took %.1fs)"
             (- (float-time) +literate-tangle--proc-start-time))
    (setq +literate-tangle--proc nil))
   ((memq (process-status process) (list 'exit 'signal))
    (+popup-buffer (get-buffer " *tangle config*"))
    (message "Failed to tangle config.org (after %.1fs)"
             (- (float-time) +literate-tangle--proc-start-time))
    (setq +literate-tangle--proc nil))))

(defun +literate-tangle-check-finished ()
  (when (and (process-live-p +literate-tangle--proc)
             (yes-or-no-p "Config is currently retangling, would you please wait a few seconds?"))
    (switch-to-buffer " *tangle config*")
    (signal 'quit nil)))
(add-hook! 'kill-emacs-hook #'+literate-tangle-check-finished)
#+end_src

** Misc
*** Native Compilation
Run emacs28's new native-compiler with ~-O3~, if available.

#+begin_src emacs-lisp
(when 'native-comp-compiler-options
  (setq native-comp-compiler-options '("-O3")))
#+end_src
*** Window Title
Set the window title to the buffer name and project folder.

#+begin_src emacs-lisp
(setq frame-title-format
      '(""
        (:eval
         (if (s-contains-p org-roam-directory (or buffer-file-name ""))
             (replace-regexp-in-string
              ".*/[0-9]*-?" "☰ "
              (subst-char-in-string ?_ ?  buffer-file-name))
           "%b"))
        (:eval
         (let ((project-name (projectile-project-name)))
           (unless (string= "-" project-name)
             (format (if (buffer-modified-p)  " ◉ %s" "  ●  %s") project-name))))))
#+end_src
*** Daemon
Use Tecosaur's greedy daemon startup.

#+begin_src emacs-lisp
(defun greedily-do-daemon-setup ()
  (require 'org)
  (require 'vertico)
  (require 'consult)
  (require 'marginalia)
  (when (require 'mu4e nil t)
    (setq mu4e-confirm-quit t)
    (setq +mu4e-lock-greedy t)
    (setq +mu4e-lock-relaxed t)
    (+mu4e-lock-add-watcher)
    (when (+mu4e-lock-available t)
      (mu4e~start))))

(when (daemonp)
  (add-hook 'emacs-startup-hook #'greedily-do-daemon-setup)
  (add-hook 'emacs-startup-hook #'init-mixed-pitch-h))
#+end_src
* Module Configuration
Configuration for Doom modules.
** Completion
#+name: doom-completion
#+begin_src emacs-lisp :tangle no
(company +childframe)
(vertico +icons)
#+end_src
*** Company
Slow down company a bit.

#+begin_src emacs-lisp
(after! company
   (setq company-idle-delay 0.1
      company-minimum-prefix-length 1
      company-selection-wrap-around t
      company-require-match 'never
      company-dabbrev-downcase nil
      company-dabbrev-ignore-case t
      company-dabbrev-other-buffers nil
      company-tooltip-limit 5
      company-tooltip-minimum-width 50))
(set-company-backend!
  '(text-mode
    markdown-mode
    gfm-mode)
  '(:seperate
    company-yasnippet
    company-ispell
    company-files))

;;nested snippets
(setq yas-triggers-in-field t)
#+end_src
** UI
#+name: doom-ui
#+begin_src emacs-lisp :tangle no
deft
doom
doom-dashboard
doom-quit
(:if IS-LINUX (emoji +unicode +github))
;; fill-column
hl-todo
ligatures
modeline
nav-flash
ophints
(popup +all +defaults)
(treemacs +lsp)
vc-gutter
window-select
workspaces
zen
#+end_src
*** Packages
#+begin_src emacs-lisp :tangle "packages.el" :comments yes
(unpin! doom-themes doom-modeline)
(package! solaire-mode :disable t)
(package! ox-chameleon :recipe (:host github :repo "tecosaur/ox-chameleon"))
#+end_src
*** Modeline
#+begin_src emacs-lisp
(after! doom-modeline
  (setq doom-modeline-major-mode-icon t
        doom-modeline-buffer-file-name-style 'truncate-with-project
        doom-modeline-modal-icon t
        inhibit-compacting-font-caches t))
#+end_src
** Editor
#+name: doom-editor
#+begin_src emacs-lisp :tangle no
(evil +everywhere)
file-templates
fold
(format +onsave)
snippets
word-wrap
#+end_src
** Emacs
#+name: doom-emacs
#+begin_src emacs-lisp :tangle no
(dired +ranger +icons)
electric
(ibuffer +icons)
(undo +tree)
vc
#+end_src
*** IBuffer
#+begin_src emacs-lisp
(setq-hook! 'ibuffer-hook ibuffer-formats
            '((mark modified read-only locked " "
                    (name 50 18 :left :elide)
                    " "
                    (size 9 -1 :right)
                    " "
                    (mode 16 16 :left :elide)
                    " " filename-and-process)
              (mark " "
                    (name 16 -1)
                    " " filename)))
#+end_src
** Term
#+name: doom-term
#+begin_src emacs-lisp :tangle no
eshell
vterm
#+end_src

Set the shell explicitly to the fish shell.

#+begin_src emacs-lisp
(setq explicit-shell-file-name (executable-find "fish"))
#+end_src

Enable ligature support.

#+begin_src emacs-lisp
(setq +ligatures-in-modes t)
#+end_src

*** ~Vterm~
Until I can fully move over to ~eshell~ like a man, setup ~vterm~.

Always compile the ~vterm~ module.

#+begin_src emacs-lisp
(setq vterm-always-compile-module t)
#+end_src

Kill the ~vterm~ buffer when the process exits.

#+begin_src emacs-lisp
(setq vterm-kill-buffer-on-exit t)
#+end_src
** Checkers
#+name: doom-checkers
#+begin_src emacs-lisp :tangle no
syntax
(spell +aspell)
grammar
#+end_src
** Tools
#+name: doom-tools
#+begin_src emacs-lisp :tangle no
(debugger +lsp)
direnv
(docker +lsp)
(eval +overlay)
gist
(lookup +devdocs +docsets)
(lsp +peek)
(magit +forge)
make
pdf
rgb
taskrunner
tmux
upload
#+end_src
*** Packages
#+begin_src emacs-lisp :tangle "packages.el" :comments yes
(unpin! lsp-mode lsp-Ui)
#+end_src
** OS
#+name: doom-os
#+begin_src emacs-lisp :tangle no
(:if IS-MAC macos)
(tty +osc)
#+end_src
** Lang
#+name: doom-lang
#+begin_src emacs-lisp :tangle no
data
emacs-lisp
(go +lsp)
(json +lsp)
(lua +fennel)
markdown
nix
(org +hugo +journal +pretty +roam2)
(rust +lsp)
(sh +fish +lsp)
(yaml +lsp)
#+end_src
** Org
Utility functions to get files in Dropbox.

#+begin_src emacs-lisp
(defun dropbox-file (path)
  (concat (getenv "HOME") "/Dropbox/" path))

(defun dropbox-files (&rest paths)
  (mapcar #'dropbox-file paths))
#+end_src

Setup basic org-mode configuration.

#+begin_src emacs-lisp
(setq org-directory (dropbox-file "org"))

(sp-local-pair
 '(org-mode)
 "<<" ">>"
 :actions '(insert))

(after! org
  (setq

   org-agenda-files
   (dropbox-files "org/inbox.org" "org/personal.org" "org/projects.org" "org/inbox_work.org" "org/work.org")

   org-todo-keywords
   '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d!)")
     (sequence"BACKLOG(b)" "PLAN(p)" "READY(r)" "ACTIVE(a)" "REVIEW(v)"
              "WAIT(w@/!)" "HOLD(h)" "|" "COMPLETED(c)""CANC(k@)"))

   org-agenda-prefix-format
   '((agenda . " %i %?-12t% s")
     (todo . " %i")
     (tags . " %i %-12:c")
     (search . " %i %-12:c"))

   org-capture-templates
   `(("p" "Personal" entry (file ,(dropbox-file "org/inbox.org")) "* TODO %?\n %i\n %a")
     ("w" "Work" entry (file+headline ,(dropbox-file "org/inbox_work.org") "To refile") "* TODO %?\n %i\n %a"))))
#+end_src

*** Packages
#+begin_src emacs-lisp :tangle "packages.el" :comments yes
(package! org-ref)
(package! org-roam-ui)
#+end_src

*** Agenda
#+begin_src emacs-lisp
(after! org-agenda
  (setq
   org-agenda-custom-commands
   '(("a" "Personal"
      ((agenda ""
               ((org-agenda-span 'day)
                (org-agenda-files (dropbox-files "org/personal.org"))
                (org-deadline-warning-days 7)
                (org-agenda-overriding-header "Agenda\n")))
       (todo "TODO"
             ((org-agenda-overriding-header "To Refile\n")
              (org-agenda-prefix-format "  ")
              (org-agenda-files (dropbox-files "org/inbox.org"))))
       (todo "NEXT"
             ((org-agenda-overriding-header "Projects\n")
              (org-agenda-prefix-format "  %c (%e) | ")
              (org-agenda-files (dropbox-files "org/projects.org"))))))
     ("w" "Work"
      ((agenda ""
               ((org-agenda-span 'day)
                (org-agenda-files (dropbox-files "org/work.org"))
                (org-deadline-warning-days 14)
                (org-agenda-overriding-header "Via\n")))
       (todo "TODO"
             ((org-agenda-overriding-header "To Refile\n")
              (org-agenda-prefix-format "  ")
              (org-agenda-files (dropbox-files "org/inbox_work.org"))))
       (todo "NEXT"
             ((org-agenda-overriding-header "Projects\n")
              (org-agenda-prefix-format "  %i %-12:c [%e] ")
              (org-agenda-files (dropbox-files "org/work.org"))))
       (todo "WAITING"
             ((org-agenda-overriding-header "Waiting on others\n")
              (org-agenda-files (dropbox-files "org/work.org")))))))))

;; Highlight additional keywords
(after! hl-todo
  (setq hl-todo-keyword-faces '(("NEXT" warning bold))))
#+end_src

*** Roam
Setup ~org-roam~ and ~deft~ to use the roam folder in Dropbox.

#+begin_src emacs-lisp
(after! org-roam
  (setq org-roam-directory (dropbox-file "roam")))

(after! deft
  (setq deft-directory (dropbox-file "roam")))
#+end_src

*** Journal
#+begin_src emacs-lisp
(after! org-journal
  (setq
   org-journal-date-prefix "#+title: "
   org-journal-file-format "%Y-%m-%d.org"
   org-journal-dir (dropbox-file "roam")
   org-journal-date-format "%Y-%m-%d"))
#+end_src
** Config
#+name: doom-config
#+begin_src emacs-lisp :tangle no
literate
(default +bindings +smartparens)
#+end_src


* Other



* To File

#+begin_src emacs-lisp

;; Appearance
(use-package frame
  :config
  (setq-default default-frame-alist
                (append (list
                         '(internal-border-width . 24)
                         '(left-fringe    . 0)
                         '(right-fringe   . 0)
                         '(tool-bar-lines . 0)
                         '(menu-bar-lines . 0)
                         ;; '(line-spacing . 0.35)
                         '(vertical-scroll-bars . nil))))
  (setq-default window-resize-pixelwise t)
  (setq-default frame-resize-pixelwise t)
  :custom
  (window-divider-default-right-width 24)
  (window-divider-default-bottom-width 12)
  (window-divider-default-places 'right-only)
  (window-divider-mode t))

(custom-set-faces!
  `(vertical-border :background ,(doom-color 'bg) :foreground ,(doom-color 'bg)))

(when (boundp 'window-divider-mode)
  (setq window-divider-default-places nil
        window-divider-default-bottom-width 0
        window-divider-default-right-width 0)
  (window-divider-mode -1))

(defadvice! fix-+evil-default-cursor-fn ()
  :override #'+evil-default-cursor-fn
  (evil-set-cursor-color (face-background 'cursor)))

(defadvice! fix-+evil-emacs-cursor-fn ()
  :override #'+evil-emacs-cursor-fn
  (evil-set-cursor-color (face-foreground 'warning)))

(add-hook 'before-make-frame-hook 'window-divider-mode)

;; remove encoding indicator
(defun doom-modeline-conditional-buffer-encoding ()
  "We expect the encoding to be LF UTF-8, so only show the modeline when this is not the case"
  (setq-local doom-modeline-buffer-encoding
              (unless (and (memq (plist-get (coding-system-plist buffer-file-coding-system) :category)
                                 '(coding-category-undecided coding-category-utf-8))
                           (not (memq (coding-system-eol-type buffer-file-coding-system) '(1 2))))
                t)))
(add-hook 'after-change-major-mode-hook #'doom-modeline-conditional-buffer-encoding) ;;remove encoding

;; Evil Mode
(setq evil-snipe-scope 'visible
      evil-want-fine-undo t
      evil-vsplit-window-right t
      evil-split-window-below t)


;; Vertico
(after! marginalia
  (setq marginalia-censor-variables nil)

  (defadvice! +marginalia--anotate-local-file-colorful (cand)
    "Just a more colourful version of `marginalia--anotate-local-file'."
    :override #'marginalia--annotate-local-file
    (when-let (attrs (file-attributes (substitute-in-file-name
                                       (marginalia--full-candidate cand))
                                      'integer))
      (marginalia--fields
       ((marginalia--file-owner attrs)
        :width 12 :face 'marginalia-file-owner)
       ((marginalia--file-modes attrs))
       ((+marginalia-file-size-colorful (file-attribute-size attrs))
        :width 7)
       ((+marginalia--time-colorful (file-attribute-modification-time attrs))
        :width 12))))

  (defun +marginalia--time-colorful (time)
    (let* ((seconds (float-time (time-subtract (current-time) time)))
           (color (doom-blend
                   (face-attribute 'marginalia-date :foreground nil t)
                   (face-attribute 'marginalia-documentation :foreground nil t)
                   (/ 1.0 (log (+ 3 (/ (+ 1 seconds) 345600.0)))))))
      ;; 1 - log(3 + 1/(days + 1)) % grey
      (propertize (marginalia--time time) 'face (list :foreground color))))

  (defun +marginalia-file-size-colorful (size)
    (let* ((size-index (/ (log10 (+ 1 size)) 7.0))
           (color (if (< size-index 10000000) ; 10m
                      (doom-blend 'orange 'green size-index)
                    (doom-blend 'red 'orange (- size-index 1)))))
      (propertize (file-size-human-readable size) 'face (list :foreground color)))))

;; Treemacs
(setq treemacs-width 40)
(setq doom-themes-treemacs-theme "doom-colors")

;; Tramp
(setq tramp-default-remote-shell "/run/current-system/sw/bin/dash"
      projectile-mode-line "Projectile")

#+end_src
